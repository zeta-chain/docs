---
title: Cross-Chain ERC-20
description: In this tutorial you will learn how to create a contract capable of minting ERC-20 tokens and sending them to a contract on a different chain using cross-chain messaging.
---

import { CodeBlock } from "~/components/shared";

## Set up your environment

```
git clone https://github.com/zeta-chain/template
cd template/contracts
yarn
```

## Create the Contract

To create a new cross-chain messaging contract you will use the `messaging`
Hardhat task available by default in the template.

```
npx hardhat messaging CrossChainERC20 to:address value:uint256
```

- `to`: address of the recipient on the destination chain
- `value`: the amount of ERC-20 tokens transferred

## Cross-Chain Messaging Contract

Let's review and make changes to the contract:

<CodeBlock language='solidity' filename="contracts/CrossChainERC20.sol" code={
`// SPDX-License-Identifier: MIT
pragma solidity 0.8.7;

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@zetachain/protocol-contracts/contracts/evm/tools/ZetaInteractor.sol";
import "@zetachain/protocol-contracts/contracts/evm/interfaces/ZetaInterfaces.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol"; // [!code highlight:2]
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";

contract CrossChainERC20 is ERC20, ERC20Burnable, ZetaInteractor, ZetaReceiver { // [!code highlight]
error InsufficientBalance();

    event CrossChainERC20Event(address, uint256);
    event CrossChainERC20RevertedEvent(address, uint256);

    ZetaTokenConsumer private immutable _zetaConsumer;
    IERC20 internal immutable _zetaToken;

    constructor(
        address connectorAddress,
        address zetaTokenAddress,
        address zetaConsumerAddress
    ) ERC20("CrossChain Token", "CCT") ZetaInteractor(connectorAddress) { // [!code highlight]
        _zetaToken = IERC20(zetaTokenAddress);
        _zetaConsumer = ZetaTokenConsumer(zetaConsumerAddress);

        _mint(msg.sender, 1000000 * 10 ** decimals()); // [!code highlight]
    }

    function sendMessage(
        uint256 destinationChainId,
        address to,
        uint256 value
    ) external payable {
        if (!_isValidChainId(destinationChainId))
            revert InvalidDestinationChainId();

        uint256 crossChainGas = 2 * (10 ** 18);
        uint256 zetaValueAndGas = _zetaConsumer.getZetaFromEth{
            value: msg.value
        }(address(this), crossChainGas);
        _zetaToken.approve(address(connector), zetaValueAndGas);

        if (balanceOf(msg.sender) < value) revert InsufficientBalance(); // [!code highlight:2]
        _burn(msg.sender, value);

        connector.send(
            ZetaInterfaces.SendInput({
                destinationChainId: destinationChainId,
                destinationAddress: interactorsByChainId[destinationChainId],
                destinationGasLimit: 300000,
                message: abi.encode(to, value, msg.sender), // [!code highlight]
                zetaValueAndGas: zetaValueAndGas,
                zetaParams: abi.encode("")
            })
        );
    }

    function onZetaMessage(
        ZetaInterfaces.ZetaMessage calldata zetaMessage
    ) external override isValidMessageCall(zetaMessage) {
        (address to, uint256 value) = abi.decode(
            zetaMessage.message,
            (address, uint256)
        );

        _mint(to, value); // [!code highlight]

        emit CrossChainERC20Event(to, value);
    }

    function onZetaRevert(
        ZetaInterfaces.ZetaRevert calldata zetaRevert
    ) external override isValidRevertCall(zetaRevert) {
        (address to, uint256 value, address from) = abi.decode( // [!code highlight:4]
            zetaRevert.message,
            (address, uint256, address)
        );

        _mint(from, value); // [!code highlight]

        emit CrossChainERC20RevertedEvent(to, value);
    }

}`
} />

By default the generated contract is a standard cross-chain messaging contract
with functions to send a message, message handler and revert handler.

Let's modify the contract to make it ERC-20 compatible.

To enable ERC-20 token features, import the necessary OpenZeppelin contracts:
`ERC20` for standard token functionality and `ERC20Burnable` to enable burning
tokens.

In the contract's constructor, initialize the ERC-20 token by setting a name and
a symbol using the `ERC20` constructor. Then, pre-mint an initial supply of
tokens to the deploying address by calling the `_mint` function.

In the `sendMessage` function, ensure the sender has sufficient tokens to send
by checking their balance. If they have enough, burn the required amount from
their balance using `_burn`. Add `msg.sender` to the message by passing it as a
third argument to `abi.decode`. This is necessary in case the transaction is
reverted, so the sender can be reimbursed.

To handle the cross-chain message, decode the incoming message in
`onZetaMessage` and mint tokens to the intended recipient using the `_mint`
function.

In the event of a message revert, decode the original message in `onZetaRevert`
to identify the sender and the amount. Reimburse the sender by minting the
tokens back to their account with `_mint`. Use the `from` address, as this is
the original sender who should receive the reimbursement.

## Deploy the Contract

Clear the cache and artifacts, then compile the contract:

```
npx hardhat compile --force
```

Run the following command to deploy the contract to two networks:

```
npx hardhat deploy --networks sepolia_testnet,bsc_testnet
```

```
ğŸš€ Successfully deployed contract on sepolia_testnet
ğŸ“œ Contract address: 0x18B6f0aB98429F00eDD44D5900090D71e3747e10

ğŸš€ Successfully deployed contract on bsc_testnet
ğŸ“œ Contract address: 0x7eA3054e5086FeE43D745975d171d3a850A94304

ğŸ”— Setting interactors for a contract on sepolia_testnet
âœ… Interactor address for 97 (bsc_testnet) is set to 0x7ea3054e5086fee43d745975d171d3a850a94304

ğŸ”— Setting interactors for a contract on bsc_testnet
âœ… Interactor address for 11155111 (sepolia_testnet) is set to 0x18b6f0ab98429f00edd44d5900090d71e3747e10
```

## Send an ERC-20 Token

Use the interact task to call the `sendMessage` method of the contract and send
ERC-20 tokens from Sepolia to BSC Testnet

```
npx hardhat interact --to 0x4955a3F38ff86ae92A914445099caa8eA2B9bA32 --value 1000000000 --contract 0x18B6f0aB98429F00eDD44D5900090D71e3747e10 --network sepolia_testnet --amount 0.01 --destination bsc_testnet
```

```
ğŸ”‘ Using account: 0x4955a3F38ff86ae92A914445099caa8eA2B9bA32

âœ… The transaction has been broadcasted to sepolia_testnet
ğŸ“ Transaction hash: 0x25cfda972e255480e15b47472ae6c8d8842201855776fedaa446f75cab7a0e86
```

You can check the broadcasted transaction on Sepolia's Etherscan:

https://sepolia.etherscan.io/tx/0x25cfda972e255480e15b47472ae6c8d8842201855776fedaa446f75cab7a0e86

Next, you can track the progress of the cross-chain transaction:

```
npx hardhat cctx 0x25cfda972e255480e15b47472ae6c8d8842201855776fedaa446f75cab7a0e86
```

```
âœ“ CCTXs on ZetaChain found.

âœ“ 0x258d3ad9adf92ac5b7d59354435adfadac0af6430737f388ebf2664c22c71158: 11155111 â†’ 97: OutboundMined
```

## Source Code

You can find the source code for the example in this tutorial here:

https://github.com/zeta-chain/example-contracts/tree/main/messaging/erc20
